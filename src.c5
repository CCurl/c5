: here (ha) @ ;
: last (la) @ ;
: immediate 1 last cell + c! ;
: inline    2 last cell + c! ;

: begin here ; immediate
: while  (jmpnz)  c, , ; immediate
: -while (njmpnz) c, , ; immediate
: until  (jmpz)   c, , ; immediate
: -until (njmpz)  c, , ; immediate
: again  (jmp)    c, , ; immediate

: if (jmpz) c, here 0 , ; immediate
: then here swap ! ; immediate

: ( a@  >t >in @ a! begin @a 0= @a+ ')' = or if a@ >in ! t> a! exit then again ; immediate

: cr 13 emit 10 emit ; inline
: space 32 emit ; inline

: <# last 32 - >t 0 t@ c! ;
: #c t@ 1- dup t! c! ;
: #n '0' + dup '9' > if 7 + then #c ;
: # base @ /mod swap #n ;
: #s begin # -while drop ;
: #> t> ;
: .  ( n-- ) <# #s #> ztype space ;
: .2 ( n-- ) <# # #s #> ztype space ;
: .3 ( n-- ) <# # # #s #> ztype space ;

: hex. ( n-- ) base @ >t 16 base ! .2 t> base ! ;
: dec. ( n-- ) base @ >t 10 base ! .3 t> base ! ;
: bin. ( n-- ) base @ >t  2 base ! .  t> base ! ;

( temp for testing )
27 . cr
last . last hex. last bin. cr
version . cr
cr

: dump    ( f t-- ) a@ >r >t a! begin @a+    . a@ t@ > until t> drop r> a! ;
: hexdump ( f t-- ) a@ >r >t a! begin @a+ hex. a@ t@ > until t> drop r> a! ;

: mil 1000 dup * * ;
: bm timer swap for next timer swap - . ;
